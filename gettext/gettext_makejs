#!/usr/bin/perl -w

use strict;
use HTML::Entities;

for (<../www/locale/*.utf8/LC_MESSAGES/messages-js.po>) {
    print $_ . "\n";
    my ($lang) = /(.*?)\.utf8/;
    print $lang . "\n";
    open(FP, $_) or die $!;
    my $out = '';
    while (<FP>) {
        next unless /^#:.*\.js/;
        my $next;
        do {
            $next = <FP>;
        } while $next =~ /^#:.*\.js/;
        my $fuzzy = 0;
        $fuzzy = 1 if $next =~ /fuzzy/;
        $next = <FP> if $next =~ /^#,/;
        my $msgid = '';
        if ($next =~ /^msgid ""\s+$/) {
            my $l;
            while (($l = <FP>) !~ /msgstr/) {
                chomp($l);
                $msgid .= substr($l, 1, -1);
            }
            $next = $l
        } else {
            ($msgid) = $next =~ /^msgid "(.*)"/;
            $next = <FP>;
        }
        my $msgstr = '';
        if (!$fuzzy) {
            if ($next =~ /^msgstr ""\s+$/) {
                while ((my $l = <FP>) !~ /^\s+$/) {
                    chomp($l);
                    $msgstr .= substr($l, 1, -1);
                }
            } else {
                ($msgstr) = $next =~ /^msgstr "(.*)"/;
            }
        }
	_decode_entities($msgstr, { nbsp => "\xc2\xa0", ocirc => "\xc3\xb4" });
        $out .= "\"$msgid\":\"$msgstr\",\n";
    }
    close FP;
    $out = substr($out, 0, -2);
    open (FP, ">$lang.utf8/LC_MESSAGES/messages.js") or die $!;
    print FP <<EOF;
/*
 * $lang.utf8/LC_MESSAGES/messages.js
 * Translation of JS strings
 * Auto-generated from .po files by gettext-makejs
 *
 * Copyright (c) 2005 UK Citizens Online Democracy. All rights reserved.
 * Email: matthew AT mysociety.org. WWW: http://www.mysociety.org/
 *
 */

var i18n = {
EOF
    print FP $out;
    print FP "\n}\n";
    close FP;
}
